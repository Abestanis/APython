import org.apache.tools.ant.taskdefs.condition.Os

import java.security.KeyException

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "com.apython.python.pythonhost"
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main {
        jniLibs.srcDirs = ['src/main/libs']
        jni.srcDirs = [] //disable automatic ndk-build
    }
    tasks.whenTaskAdded {
        task ->
            if (task.name == 'compileDebugNdk') {
                //noinspection GroovyAssignabilityCheck
                task.dependsOn ndkBuild
            }
    }
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkDir = properties.getProperty('ndk.dir')
    def executable = 'sh'
    def firstArg   = '-c'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        executable = 'cmd'
        firstArg   = '/c'
    }
    //noinspection GroovyAssignabilityCheck
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK.') {
        workingDir file('src/main')
        inputs.dir file('src/main/jni')
        outputs.dir file('src/main/libs')
        commandLine executable, firstArg, new File("$ndkDir", 'ndk-build').getAbsolutePath(), 'NDK_OUT=' + getBuildDir().absolutePath.replace('\\', '/') + '/intermediates/ndk/obj'
    }
    ndkBuild.onlyIf {
        if (ndkDir == null) {
            throw new KeyException('NDK location not found! Define the location with ndk.dir in the local.properties file.')
        }
        true
    }
    ndkBuild.finalizedBy {
        copyPythonExecutable
    }
    //noinspection GroovyAssignabilityCheck
    task copyPythonExecutable(type: Copy, description: 'Copys the Python executable to the raw directory.', dependsOn: 'ndkBuild') {
        from new File(project.getProjectDir().absolutePath + '\\src\\main\\libs\\armeabi-v7a\\python')
        into 'src/main/res/raw'
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.4.0'
}
